# Maintainer: Jan Veen <jan.veen Ã¦t posteo.de>
pkgname=tiny-auth
pkgver=0.8.2
pkgrel=2
pkgdesc="OIDC Provider"
arch=('x86_64')
url="https://j.njsm.de/git/veenj/${pkgname}"
license=('GPL3')
depends=('gcc-libs' 'systemd' 'openssl')
makedepends=('rust' 'gcc' 'pkgconf' 'npm')
backup=("etc/${pkgname}/config.yml"
        "etc/${pkgname}/log4rs.yml"
        "etc/${pkgname}/store/scopes/address.yml"
        "etc/${pkgname}/store/scopes/email.yml"
        "etc/${pkgname}/store/scopes/openid.yml"
        "etc/${pkgname}/store/scopes/phone.yml"
        "etc/${pkgname}/store/scopes/profile.yml")

build() {
    cd "$srcdir"
    chmod u+rw . -R
    cd src/rust
    cargo build --release
    cd ../..
    cd src/typescript
    npm run build
}

check() {
    cd "$srcdir"
    cd src/rust
    cargo fmt --all -- --check
    cargo clippy --release -j 8 -- -D clippy::all
    cargo test -j 1
}

package() {
    cd "$srcdir"

    cd src/rust
    install -D -m 755 target/release/${pkgname} $pkgdir/usr/bin/${pkgname}
    install -D -m 755 target/release/${pkgname}-password-encoder $pkgdir/usr/bin/${pkgname}-password-encoder
    install -D -m 755 target/release/${pkgname}-scope-debugger $pkgdir/usr/bin/${pkgname}-scope-debugger

    cd ../..
    install -D -m 644 pkg/config.yml $pkgdir/etc/${pkgname}/config.yml
    install -D -m 644 pkg/log4rs.yml $pkgdir/etc/${pkgname}/log4rs.yml
    install -D -m 644 pkg/systemd.service $pkgdir/usr/lib/systemd/system/${pkgname}.service
    install -D -m 644 pkg/systemd.sysusers $pkgdir/usr/lib/sysusers.d/${pkgname}.conf
    install -D -m 644 pkg/systemd.tmpfiles $pkgdir/usr/lib/tmpfiles.d/${pkgname}.conf

    mkdir -p $pkgdir/usr/share/${pkgname}/static/css
    cp -r src/static/css $pkgdir/usr/share/${pkgname}/static
    mkdir -p $pkgdir/usr/share/${pkgname}/static/templates
    cp -r src/static/templates $pkgdir/usr/share/${pkgname}/static
    rm $pkgdir/usr/share/${pkgname}/static/templates/index.html.j2
    mkdir -p $pkgdir/usr/share/${pkgname}/static/img
    cp -r src/static/img $pkgdir/usr/share/${pkgname}/static
    mkdir -p $pkgdir/usr/share/${pkgname}/static/js
    cp -r src/typescript/dist/* $pkgdir/usr/share/${pkgname}/static/js
    cp -r src/typescript/dist/index.html $pkgdir/usr/share/${pkgname}/static/templates/index.html.j2
    rm $pkgdir/usr/share/${pkgname}/static/js/index.html

    mkdir -p $pkgdir/etc/${pkgname}/store/{users,clients,scopes}
    cp -r pkg/scopes/* $pkgdir/etc/${pkgname}/store/scopes
    cp -r pkg/clients/* $pkgdir/etc/${pkgname}/store/clients

    mkdir -p $pkgdir/usr/share/doc/${pkgname}
    cp -r doc/* $pkgdir/usr/share/doc/${pkgname}
}
