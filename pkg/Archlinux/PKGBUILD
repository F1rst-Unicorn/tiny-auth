# Maintainer: Jan Veen <jan.veen Ã¦t posteo.de>
pkgname=tiny-auth
pkgver=0.1.0
pkgrel=1
pkgdesc="OIDC Provider"
arch=('x86_64')
url="https://gitlab.com/veenj/${pkgname}"
license=('GPL3')
depends=('gcc-libs' 'systemd' 'openssl')
makedepends=('rust' 'gcc' 'pkgconf')
backup=("etc/${pkgname}/config.yml"
        "etc/${pkgname}/log4rs.yml")

build() {
    cd "$srcdir"
    chmod u+rw . -R
    cargo build --release
}

check() {
    cd "$srcdir"
    cargo fmt --all -- --check
    cargo clippy --release -j 8 -- -D clippy::all
    cargo test -j 1
}

package() {
    cd "$srcdir"

    install -D -m 755 target/release/${pkgname} $pkgdir/usr/bin/${pkgname}
    install -D -m 644 pkg/config.yml $pkgdir/etc/${pkgname}/config.yml
    install -D -m 644 pkg/log4rs.yml $pkgdir/etc/${pkgname}/log4rs.yml
    install -D -m 644 pkg/systemd.service $pkgdir/usr/lib/systemd/system/${pkgname}.service
    install -D -m 644 pkg/systemd.sysusers $pkgdir/usr/lib/sysusers.d/${pkgname}.conf
    install -D -m 644 pkg/systemd.tmpfiles $pkgdir/usr/lib/tmpfiles.d/${pkgname}.conf

    mkdir -p $pkgdir/usr/share/${pkgname}/static
    cp -r static $pkgdir/usr/share/${pkgname}

    mkdir -p $pkgdir/etc/${pkgname}/store/{users,clients}

    mkdir -p $pkgdir/usr/share/doc/${pkgname}
    cp -r doc/* $pkgdir/usr/share/doc/${pkgname}
}
